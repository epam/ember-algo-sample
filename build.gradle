plugins {
    // Supports functionality similar to Maven BOM.
    // Helps to avoid re-declaring dependency version in each subproject.
    // See https://github.com/spring-gradle-plugins/dependency-management-plugin
    id "io.spring.dependency-management" version "1.1.5" apply false
}


apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

apply from: "$rootDir/gradle/deltix.gradle"
apply from: "$rootDir/gradle/docker-support.gradle"

repositories {
    deltixMavenRepository("epm-rtc-public-java")
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

ext {
    emberVersion = '1.14.161'
}

dependencyManagement {
    imports {
        mavenBom 'deltix:deltix-ember-bom:' + emberVersion
    }
}

dependencies {
    // Ember API
    implementation "deltix:deltix-anvil-lang"
    implementation "deltix:deltix-ember-api"
    implementation "deltix:deltix-ember-algo-api"
    implementation "deltix:deltix-ember-message"
    implementation "deltix:deltix-ember-codec"
    implementation "deltix:deltix-anvil-cluster"
    implementation "deltix:deltix-efix-core"

    // Centralized security master API
    implementation 'deltix:sm-messages'
    implementation 'deltix:deltix-sm-api'

    // Decimal datatype
    implementation 'com.epam.deltix:dfp'

    // GC Free Logging
    implementation 'com.epam.deltix:gflog-api'

    // New Order Book API
    implementation "deltix.orderbook:orderbook-core"

    // Classic Order Book API
    implementation 'deltix:deltix-quoteflow'
    implementation 'deltix:deltix-containers'

    implementation 'rtmath:rtmath-finanalysis'

    // Deltix Collections are used in some TimeBase messages
    implementation "deltix.qsrv:deltix-commons-collections"
    implementation "deltix.qsrv:deltix-commons-util"
    implementation "deltix.qsrv:deltix-commons-lang"

    implementation "deltix.qsrv:deltix-timebase-api-messages"


    // @Nullable, etc.
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    // Optional HDR Histogram for latency monitoring
    implementation 'org.hdrhistogram:HdrHistogram:2.1.12'

    // Unit tests
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    testImplementation 'org.mockito:mockito-core:2.6.3'
    testImplementation "deltix:deltix-ember-message-bus-client"
    testImplementation "deltix:deltix-ember-algo-test:$emberVersion"
    // We need this if we want to Run/Debug algo from this project
    testImplementation "deltix:deltix-ember-app:$emberVersion"
}

test {
    // useJUnitPlatform() // if you're using JUnit 5
    maxHeapSize = '4G'
    minHeapSize = '4G'

    jvmArgs '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED',
            '--add-opens=java.base/sun.nio=ALL-UNNAMED',
            '--add-opens=java.base/java.nio=ALL-UNNAMED',
            '--add-opens=java.base/java.lang=ALL-UNNAMED',
            '--add-opens=java.base/java.util.zip=ALL-UNNAMED',
            '--add-opens=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED'
}

// ZIP with sample algorithms to share with clients (result file can be found under build/distributions/*.zip)
task makeZIP(type: Zip) {
    archiveBaseName = 'deltix-aglo-sample'

    from('.') {
        include '**/*'
        exclude '.git/'
        exclude '.gradle/'
        exclude '.idea/libraries/'
        exclude '.idea/vcs.xml'
        exclude 'build/'
        exclude 'out/'
    }
}

// Docker support

ext {
    dockerImageName = 'ember/ember-with-custom-algo'
}

ext.dockerFileReplaceTokens = [
    emberDockerImageVersion: emberVersion
]

// Use :dockerBuildImage to make a docker image
task prepareFilesForDocker(type: Copy) {
    group 'docker'
    dependsOn ':jar'

    def outputDir = file("${buildDir}/docker/custom")

    from jar
    into outputDir

}
